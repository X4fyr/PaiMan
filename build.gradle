group 'de.de.x4fyr'
version '0.2'

buildscript {
    ext.kotlin_version = '1.1.2'
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:3.1.1'
        classpath 'com.google.firebase:firebase-plugins:1.1.1'
    }
}


ext.gitVersioner = [
        defaultBranch           : "master",  // default "master"
        stableBranches          : [], // default [], the feature branch postfix (-dm4(6)) will not be appended
        // on stable branches, all commits are included into the version number calculation
        yearFactor              : 1000,       // default "1000", increasing every 8.57h
        snapshotEnabled         : hasProperty('snapshot') ? snapshot.toBoolean() : true,      // default true, the
        // "-SNAPSHOT"
        // postfix
        localChangesCountEnabled: false,       // default true, the (<commitCount>) before -SNAPSHOT
        currentBranch           : hasProperty('branch') ? branch : null,
        shortName               : { gitVersion ->            // optional closure to build a short name
            // allows you to add your own short name logic
            // All properties from gitVersion are available
            // can be used for CI `System.getenv("BUILD_NUMBER")`

            // i.e. use short sha1
            return gitVersion.branchName // .commit.subSequence(0, 7)
        }
]
apply from: './versioner.gradle'
project.version += ".$gitVersionName"
println("version: $project.version")

apply plugin: 'idea'

task copyJavaFxApp(type: Copy) {
    from('app/build/distributions') {
        rename ".*.zip", "paiman-${version}.zip"
    }
    into 'archive/'
    include "*.zip"
    dependsOn "app:distZip"
}

task copyAndroidApp(type: Copy) {
    from('android/build/outputs/apk/') {
        rename ".*.apk", "paiman-${version}.apk"
    }
    into 'archive/'
    include "*-release.apk"
    dependsOn "android:assembleRelease"
}

task copyArtifacts() {
    dependsOn "copyJavaFxApp", "copyAndroidApp"
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}
