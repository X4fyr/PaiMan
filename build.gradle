group 'de.de.x4fyr'

ext.major_version = 0
ext.minor_version = 3
ext.revision_number = 1
version "$major_version.$minor_version.$revision_number"
println("version: $version")

buildscript {
    ext.kotlin_version = '1.2.60'
    ext.dagger_version = '2.13'
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
        maven {
            url 'http://dl.bintray.com/kotlin/kotlin-eap/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:3.1.1'
        classpath 'com.google.firebase:firebase-plugins:1.1.1'
    }
}

apply plugin: 'idea'

task copyJavaFxApp(type: Copy) {
    from('app/build/distributions') {
        rename ".*.zip", "paiman-${version}.zip"
    }
    into 'archive/'
    include "*.zip"
    dependsOn "app:distZip"
}

task copyAndroidApp(type: Copy) {
    from('android/build/outputs/apk/') {
        rename ".*.apk", "paiman-${version}.apk"
    }
    into 'archive/'
    include "*-release.apk"
    dependsOn "android:assembleRelease"
}

task copyArtifacts() {
    dependsOn "copyJavaFxApp", "copyAndroidApp"
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
        maven {
            url 'http://dl.bintray.com/kotlin/kotlin-eap/'
        }
        flatDir {
            dirs 'libs'
        }
    }
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}
